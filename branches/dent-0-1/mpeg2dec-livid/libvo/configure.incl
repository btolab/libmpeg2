
dnl 
dnl Display driver probing...
dnl

AC_ARG_ENABLE(3dfx,
  [  --disable-3dfx		make a version not using experimental 3dfx driver],
  enable_3dfx=no, enable_3dfx=yes)
if test x$enable_3dfx = xyes; then
	AC_MSG_CHECKING(for /dev/3dfx)

	if test -c "/dev/3dfx"; then
		have_3dfx=yes
		AC_MSG_RESULT(found)
		X_LIBS="$X_LIBS -lXxf86dga"
		LIBS_VIDEO_OUTPUT="$LIBS_VIDEO_OUTPUT display_3dfx.la"
		AC_DEFINE(HAVE_3DFX,1,[Define if MGA GX00 is installed])
	else
		have_3dfx=no
		AC_MSG_RESULT(not found)
	fi
fi

AM_CONDITIONAL(HAVE_3DFX, test x$have_3dfx = "xyes")


dnl
dnl Matrox Millenium GX00 BES and YUV2RGB support
dnl

AC_ARG_ENABLE(mga,
  [  --disable-mga		make a version not using MGA],
  enable_mga=no, enable_mga=yes)
if test x$enable_mga = xyes; then
	AC_MSG_CHECKING(for /dev/mga_vid)

	if test -c "/dev/mga_vid"; then
		have_mga=yes
		AC_MSG_RESULT(found)
		LIBS_VIDEO_OUTPUT="$LIBS_VIDEO_OUTPUT display_mga.la"
		AC_DEFINE(HAVE_MGA,1,[Define if MGA GX00 is installed])
	else
		have_mga=no
		AC_MSG_RESULT(not found)
dnl		AC_MSG_WARN("to create /dev/mga_vid - use mknod /dev/mga_vid c 178 0")
	fi
fi

AM_CONDITIONAL(HAVE_MGA, test x$have_mga = "xyes")


dnl SDL option added by Ryan C. Gordon 04/23/2000
dnl   !!! This really needs to check the SDL version.
dnl   !!! I think we're using SDL 1.1 features, so 1.0 won't cut it.

AC_ARG_ENABLE(sdl,
  [  --disable-sdl		make a version not using SDL],
  enable_sdl=no, enable_sdl=yes)
if test "x$enable_sdl" = xyes; then
	AC_CHECK_PROG(SDLCONFIG, sdl-config, yes)

	if test "x$SDLCONFIG" = xyes; then
		have_sdl=yes
		AC_DEFINE(HAVE_SDL)
		LIBS_VIDEO_OUTPUT="$LIBS_VIDEO_OUTPUT display_sdl.la"
		LIBVO_CFLAGS="$LIBVO_CFLAGS `sdl-config --cflags`"
		LIBVO_CONFIG_LIBS="$LIBVO_CONFIG_LIBS `sdl-config --libs`"
	else
		have_sdl=no
	fi
fi

AM_CONDITIONAL(HAVE_SDL, test x$have_sdl = "xyes")

dnl
dnl Check for X11 lib
dnl

dnl These are in the 'top-level' configure
dnl AC_PATH_X
dnl AC_PATH_XTRA

if test "x$no_x" = "xyes"; then
	have_x11=no
else
	have_x11=yes
fi


AM_CONDITIONAL(HAVE_X11,test x$have_x11 = "xyes")

if test "x$have_x11_lib" = "xyes"; then
	AC_DEFINE(HAVE_X11)
	LIBS_VIDEO_OUTPUT="$LIBS_VIDEO_OUTPUT display_x11.la"
fi


dnl
dnl Checks for libraries. (Xv)
dnl

have_xv=no
AC_ARG_ENABLE(xv,
  [  --disable-xv		make a version not using Xv],
  enable_xv=no, enable_xv=yes)
if test "x$enable_xv" = "xyes"; then
AC_CHECK_LIB(Xv, XvShmCreateImage, 
	X_LIBS="$X_LIBS -lXv"
	have_xv=yes
	AC_DEFINE(HAVE_XV),,
		-L$x_libraries -lXext -lX11)
fi


dnl
dnl Checks for libraries. (Xxf86dga)
dnl

have_dga=no
AC_ARG_ENABLE(dga,
  [  --disable-dga		make a version not using DGA],
  enable_dga=no, enable_dga=yes)
if test "x$enable_dga" = "xyes"; then
	AC_CHECK_LIB(Xxf86dga, XF86DGAQueryExtension,
		X_LIBS="$X_LIBS -lXxf86dga"
		have_dga=yes
		AC_DEFINE(HAVE_DGA),,
		-L$x_libraries -lXext -lX11)
fi


dnl
dnl There must be a better way to check for SMP....
dnl

AC_MSG_CHECKING(for SMP)
AC_SUBST(MOD_CFLAGS)
if uname -a | grep SMP > /dev/null; then
	AC_MSG_RESULT(yes)
	MOD_CFLAGS=-D__SMP__
else
	AC_MSG_RESULT(no)
fi


dnl
dnl fill in all libraries which should be built
dnl

AC_SUBST(X_LIBS)
AC_SUBST(LIBS_VIDEO_OUTPUT)

echo "
output video
------------
x11			: ${have_x11}
	xv		: ${have_xv}
	dga		: ${have_dga}
SDL			: ${have_sdl}
3dfx			: ${have_3dfx}
matrox g200/g400	: ${have_mga}
" >> config
