AC_SUBST(LIBVO_CFLAGS)
AC_SUBST(LIBVO_CONFIG_LIBS)
AC_SUBST(LIBVO_CONFIG_OBJS)

dnl 
dnl Display driver probing...
dnl

AC_ARG_ENABLE(3dfx,
  [  --enable-3dfx           make a version using experimental 3dfx driver],
  enable_3dfx=yes, enable_3dfx=no)
if test x$enable_3dfx = xyes; then
	AC_DEFINE(HAVE_3DFX)
  AC_MSG_CHECKING(for /dev/3dfx)
  if test -c "/dev/3dfx"; then
    AC_MSG_RESULT(found)
		X_LIBS="$X_LIBS -lXxf86dga"
  else
    AC_MSG_RESULT(not found)
  fi
fi

AC_ARG_ENABLE(mga,
  [  --disable-mga           make a version not using MGA],
  enable_mga=no, enable_mga=yes)
if test x$enable_mga = xyes; then
	AC_DEFINE(HAVE_MGA)
	AM_CONDITIONAL(HAVE_MGA,true)
  AC_MSG_CHECKING(for /dev/mga_vid)
  if test -c "/dev/mga_vid"; then
    AC_MSG_RESULT(found)
  else
    AC_MSG_RESULT(not found)
		AC_MSG_WARN("to create /dev/mga_vid - use mknod /dev/mga_vid c 178 0")
  fi
else
	AM_CONDITIONAL(HAVE_MGA,false)
fi

if test x$enable_mlib = x; then
AC_ARG_ENABLE(mlib,
  [  --disable-mlib          make a version not using mediaLib],
  enable_mlib=no, enable_mlib=yes)
fi
if test x$enable_mlib = xyes; then
  AC_CHECK_LIB(mlib, main, 
               [ LIBS="$LIBS -L/opt/SUNWmlib/lib -lmlib" 
                 LIBVO_CONFIG_OBJS="$LIBVO_CONFIG_OBJS yuv2rgb_mlib.lo" 
                 LIBVO_CFLAGS="$LIBVO_CFLAGS -I/opt/SUNWmlib/include" 
                 AC_DEFINE(HAVE_MLIB) ],
	       , -L/opt/SUNWmlib/lib)

fi


dnl SDL option added by Ryan C. Gordon 04/23/2000
dnl   !!! This really needs to check the SDL version.
dnl   !!! I think we're using SDL 1.1 features, so 1.0 won't cut it.
AC_ARG_ENABLE(sdl, [  --enable-sdl            make a version using SDL],
  enable_sdl=yes, enable_sdl=no)
if test "x$enable_sdl" = xyes; then
  AC_CHECK_PROG(SDLCONFIG, sdl-config, yes)
  if test "x$SDLCONFIG" = xyes; then
		AC_DEFINE(HAVE_SDL)
    LIBVO_CFLAGS="$LIBVO_CFLAGS `sdl-config --cflags`"
    LIBVO_CONFIG_LIBS="$LIBVO_CONFIG_LIBS `sdl-config --libs`"
  else
    AC_MSG_ERROR(["--enable-sdl" specified, but sdl-config isn't in your path!])
  fi
fi

dnl FIXME check for X actually existing
AC_DEFINE(HAVE_X11)

dnl Checks for libraries. (X11)
AC_PATH_XTRA
AC_CHECK_LIB(Xv, XvShmCreateImage, 
	X_LIBS="$X_LIBS -lXv"
	AC_DEFINE(HAVE_XV),,
	$X_LIBS $X_PRE_LIBS -lXext -lX11 $X_EXTRA_LIBS)


dnl There must be a better way to check for SMP....
AC_MSG_CHECKING(for SMP)
AC_SUBST(MOD_CFLAGS)
if uname -a | grep SMP >/dev/null; then
	AC_MSG_RESULT(yes)
	MOD_CFLAGS=-D__SMP__
else
	AC_MSG_RESULT(no)
fi

dnl Do the right thing for architecture specific goodness
case "$host_alias" in
i?86-* | k?-*) dnl catch i386,i486,i586,i686,k6,k7
	LIBVO_CONFIG_OBJS="$LIBVO_CONFIG_OBJS yuv2rgb_mmx.lo" ;;
alpha*-*) ;;
sparc-*) ;;
ppc-*) ;;
esac

