AC_SUBST([LIBVO_CFLAGS])
AC_SUBST([LIBVO_LIBS])
AC_SUBST([LIBVO_PLUGINS])

dnl always build these plugins
LIBVO_PLUGINS="${LIBVO_PLUGINS} display_null.la display_md5.la display_pgm.la"

dnl check for X11
AC_PATH_XTRA
if test x"$no_x" != x"yes"; then
    AC_DEFINE([LIBVO_X11],,[libvo X11 support])
    LIBVO_CFLAGS="$LIBVO_CFLAGS $X_CFLAGS"
    LIBVO_LIBS="$LIBVO_LIBS $X_PRE_LIBS $X_LIBS -lX11 $X_EXTRA_LIBS"
    LIBVO_PLUGINS="${LIBVO_PLUGINS} display_x11.la"

    dnl check for Xshm
    AC_CHECK_LIB([Xext],[XShmCreateImage],
	[AC_DEFINE([LIBVO_XSHM],,[libvo Xshm support])
	LIBVO_LIBS="$LIBVO_LIBS -lXext"],,
	[$X_PRE_LIBS $X_LIBS -lX11 $X_EXTRA_LIBS])

    dnl check for XV
    AC_CHECK_LIB([Xv],[XvCreateImage],
	[AC_DEFINE([LIBVO_XV],,[libvo XV support])
	LIBVO_LIBS="$LIBVO_LIBS -lXv"],,
	[$X_PRE_LIBS $X_LIBS -lX11 $X_EXTRA_LIBS -lXext])
fi

dnl check for SDL
dnl   !!! This really needs to check the SDL version. 
dnl   !!! I think we're using SDL 1.1 features, so 1.0 won't cut it. 
AC_ARG_ENABLE([sdl],[  --disable-sdl           make a version not using SDL])
if test x"$enable_sdl" != x"no"; then
    AC_CHECK_PROG([SDLCONFIG],[sdl-config],[yes])
    if test x"$SDLCONFIG" = x"yes"; then
	AC_DEFINE([LIBVO_SDL],,[libvo SDL support])
	LIBVO_CFLAGS="$LIBVO_CFLAGS `sdl-config --cflags`"
	LIBVO_LIBS="$LIBVO_LIBS `sdl-config --libs`"
	LIBVO_PLUGINS="${LIBVO_PLUGINS} display_sdl.la"
    fi
fi

dnl check for mlib
AC_ARG_ENABLE([mlib],
    [  --disable-mlib          make a version not using mediaLib])
if test x"$enable_mlib" != x"no"; then
    AC_CHECK_LIB([mlib],[mlib_VideoColorYUV2RGB420],
	[AC_DEFINE([LIBVO_MLIB],,[libvo mlib support])
	LIBVO_CFLAGS="$LIBVO_CFLAGS -I/opt/SUNWmlib/include"
	LIBVO_LIBS="$LIBVO_LIBS -L/opt/SUNWmlib/lib -lmlib"],,
	[-L/opt/SUNWmlib/lib])
fi

dnl the following hardware drivers are EXPERIMENTAL.
dnl we do not attempt to make real checks for them.

dnl check for MGA - EXPERIMENTAL
AC_ARG_ENABLE([mga],[  --enable-mga            make a version using MGA],
    [AC_DEFINE([LIBVO_MGA],,[libvo MGA support])
    LIBVO_PLUGINS="${LIBVO_PLUGINS} display_mga.la"])

dnl check for ggi - EXPERIMENTAL
AC_ARG_ENABLE([ggi],[  --enable-ggi            make a version using ggi],
    [AC_DEFINE([LIBVO_GGI],,[libvo ggi support])
    LIBVO_LIBS="$LIBVO_LIBS -lggi"
    LIBVO_PLUGINS="${LIBVO_PLUGINS} display_ggi.la"])
