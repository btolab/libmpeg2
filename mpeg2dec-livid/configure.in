dnl Autoconf configuration script mpeg2dec
dnl 
dnl Aaron Holtzman - May 1999
dnl
AC_INIT(mpeg2dec.c)
AC_CANONICAL_HOST
AC_PREREQ(2.13)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(mpeg2dec, 0.1.7-cvs)

AM_MAINTAINER_MODE
AC_PROG_CC
AC_PROG_GCC_TRADITIONAL
AC_PROG_RANLIB
AC_PROG_LN_S
AC_C_BIGENDIAN

AC_LIBTOOL_DLOPEN
AM_DISABLE_STATIC
AM_PROG_LIBTOOL


dnl
dnl Add pathname where linux header files are (optional)
dnl
AC_ARG_WITH(kernel-includes,
    [  --with-kernel-includes  using kernel header files],
    [case "$withval" in
      yes)
        KERNEL_INCLUDES="-I/usr/src/linux/include"
      ;;
      *)
        KERNEL_INCLUDES="-I$withval"
      ;;
    esac],[
    KERNEL_INCLUDES="-I/usr/src/linux/include"
])
AC_SUBST(KERNEL_INCLUDES)


dnl 
dnl Display driver probing...
dnl

AC_ARG_ENABLE(3dfx,
  [  --enable-3dfx           make a version using experimental 3dfx driver],
  enable_3dfx=yes, enable_3dfx=no)
if test "x$DISPLAY_DRIVER" = x -a x$enable_3dfx = xyes; then
  AC_MSG_CHECKING(for /dev/3dfx)
  if test -c "/dev/3dfx"; then
    AC_MSG_RESULT(found)
    DISPLAY_DRIVER=display_3dfx.o
  else
    AC_MSG_RESULT(not found)
  fi
fi

AC_ARG_ENABLE(mga,
  [  --disable-mga           make a version not using MGA],
  enable_mga=no, enable_mga=yes)
if test "x$DISPLAY_DRIVER" = x -a x$enable_mga = xyes; then
  AC_MSG_CHECKING(for /dev/mga_vid)
  if test -c "/dev/mga_vid"; then
    AC_MSG_RESULT(found)
    DISPLAY_DRIVER=display_mga_vid.o
  else
    AC_MSG_RESULT(not found)
  fi
fi
AM_CONDITIONAL(BUILD_MGA_DRIVER, test "$DISPLAY_DRIVER" = "display_mga_vid.o")


AC_ARG_ENABLE(gatos, 
  [  --enable-gatos          make a version using GATOS],
  enable_gatos=yes, enable_gatos=no)
if test "x$DISPLAY_DRIVER" = x -a x$enable_gatos = xyes; then
   AC_CHECK_LIB(gatos, main, [ DISPLAY_DRIVER=display_gatos.o 
	   LIBS="$LIBS -lgatos" ], , -L/usr/X11R6/lib -L/usr/lib -L/usr/local/lib)
fi

AC_ARG_ENABLE(xil, [  --disable-xil           make a version not using XIL],
  enable_xil=no, enable_xil=yes)
if test "x$DISPLAY_DRIVER" = x -a x$enable_xil = xyes; then
  AC_CHECK_LIB(xil, main, [ DISPLAY_DRIVER="display_xil.o yuv2rgb.o" 
                            LIBS="$LIBS -lxil" ])
fi


dnl SDL option added by Ryan C. Gordon 04/23/2000
dnl   !!! This really needs to check the SDL version.
dnl   !!! I think we're using SDL 1.1 features, so 1.0 won't cut it.
AC_ARG_ENABLE(sdl, [  --enable-sdl            make a version using SDL],
  enable_sdl=yes, enable_sdl=no)
if test "x$enable_sdl" = xyes; then
  AC_CHECK_PROG(SDLCONFIG, sdl-config, yes)
  if test "x$SDLCONFIG" = xyes; then
    DISPLAY_DRIVER="display_sdl.o"
    ROOT_CFLAGS="$ROOT_CFLAGS `sdl-config --cflags`"
    LIBS="$LIBS `sdl-config --libs`"
  else
    AC_MSG_ERROR([\"--enable-sdl\" specified, but sdl-config isn't in your path!])
  fi
fi

if test "x$DISPLAY_DRIVER" = x; then
  AC_MSG_WARN(no accelerated display driver found -> using X11)
  DISPLAY_DRIVER="display_x11.o yuv2rgb.o"

	dnl Checks for libraries. (X11)
	AC_PATH_XTRA
	AC_CHECK_LIB(Xv, XvShmCreateImage, 
		X_LIBS="$X_LIBS -lXv"
		ROOT_CFLAGS="$ROOT_CFLAGS -DHAVE_XV",,
		$X_LIBS $X_PRE_LIBS -lXext -lX11 $X_EXTRA_LIBS)
	AM_CONDITIONAL(HAVE_X, test "x$no_x" != "xyes")

fi

dnl
dnl Other extentions
dnl

AC_ARG_ENABLE(mlib,
  [  --disable-mlib          make a version not using mediaLib],
  enable_mlib=no, enable_mlib=yes)
if test x$enable_mlib = xyes; then
  AC_CHECK_LIB(mlib, main,
    [ EXTENSION_DRIVER="$EXTENSION_DRIVER yuv2rgb_mlib.o motion_comp_mlib.o"
      LIB_CONFIG_OBJS="$LIB_CONFIG_OBJS idct_mlib.o"
      LIBS="$LIBS -L/opt/SUNWmlib/lib -R/opt/SUNWmlib/lib -lmlib"
      CFLAGS="$CFLAGS -I/opt/SUNWmlib/include"
      AC_DEFINE(HAVE_MLIB)], , -L/opt/SUNWmlib/lib )
fi

CONFIG_OBJS="$CONFIG_OBJS $DISPLAY_DRIVER $EXTENSION_DRIVER"
AC_SUBST(CONFIG_OBJS)
AC_SUBST(LIB_CONFIG_OBJS)

dnl There must be a better way to check for SMP....
AC_MSG_CHECKING(for SMP)
AC_SUBST(MOD_CFLAGS)
if uname -a | grep SMP >/dev/null; then
	AC_MSG_RESULT(yes)
	MOD_CFLAGS=-D__SMP__
else
	AC_MSG_RESULT(no)
fi


dnl Figure out which OS we're on and what to do about it
case "$host" in
*-linux*) ;;
*-solaris*) ;;
*) ;;
esac

dnl Set the appropriate architecture define
case "$host" in
i?86-*)
	AC_DEFINE(__i386__) 
	LIB_CONFIG_OBJS="$LIB_CONFIG_OBJS idct_mmx.lo idct_block_mmx.lo motion_comp_mmx.lo"
	GLOBAL_CFLAGS="$GLOBAL_CFLAGS -march=pentiumpro";;
alpha*-*) AC_DEFINE(__alpha__);;
sparc-*) AC_DEFINE(__sparc__);;
ppc-*) AC_DEFINE(__ppc__);;
*) echo "$host is not currently supported by mpeg2dec"; exit 1;;
esac

AC_SUBST(GLOBAL_CFLAGS)
AC_SUBST(ROOT_CFLAGS)
AC_SUBST(LIBMPEG2_CFLAGS)
AC_SUBST(TOOLS_CFLAGS)

AC_OUTPUT( libmpeg2/Makefile tools/Makefile drivers/Makefile Makefile ) 
